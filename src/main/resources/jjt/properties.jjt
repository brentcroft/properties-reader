
options {
    OUTPUT_DIRECTORY = "src/main/java/com/brentcroft/pxr/parser";
    ERROR_REPORTING = true;
    UNICODE_INPUT = true;
    STATIC = false;
}

PARSER_BEGIN( PxrParser )
package com.brentcroft.pxr.parser;

import static com.brentcroft.pxr.PxrUtils.nonNull;
import static com.brentcroft.pxr.PxrUtils.isNull;

import com.brentcroft.pxr.model.PxrComment;
import com.brentcroft.pxr.model.PxrEntry;
import com.brentcroft.pxr.model.PxrEntryContinuation;
import com.brentcroft.pxr.model.PxrItem;
import com.brentcroft.pxr.model.PxrProperties;

import java.util.ArrayList;
import java.util.List;

import static java.lang.String.format;

public class PxrParser implements PxrItem
{
}
PARSER_END( PxrParser )

TOKEN :
{
    <LF: "\n">
    | <CR: "\r">
    | <SPACE:  ( " " )+ >
    | <BANG:  "!" ( <SPACE> )? >
    | <HASH:  "#" ( <SPACE> )? >
    | <EQUALS:  "=" >
    | <COLON:  ":" >
    | <CONT:  "\\" ( <LF> | <CR> <LF> ) >
    | <CHAR: ( ~[ "\n", "\r", "!", "#", "=", ":", " ", "\\" ] )+ >
}

PxrProperties parse() :
{
    String space = null;
    String init = null;
    String text = null;
    boolean eol = false;
    String key = null;
    String sep = null;
    PxrComment comment = null;
    List<PxrEntryContinuation> pecs = null;
    PxrProperties pxrProperties = new PxrProperties();
    int entryIndex = 1;
}
{
    (
        (
            (
                ( eol = eol() )
                | ( space = space() )
                | init = initiator() ( LOOKAHEAD(2) text = comment() )? ( LOOKAHEAD(2) eol = eol() )?
                | key = key() ( LOOKAHEAD(2) sep = sep() ( LOOKAHEAD(2) text = text() ( pecs = continuations() )? )? )? ( LOOKAHEAD(2) eol = eol() )?
            )
            {
                if ( isNull( key ) )
                {
                    if (isNull( comment ))
                    {
                        comment = new PxrComment();
                        comment.setKey( FOOTER_KEY );
                    }
                    // maybe header
                    else if (
                            isNull( pxrProperties.getHeader() ) && pxrProperties.isEmpty()
                            && comment.isCharactersWritten() && comment.isEol()
                            && eol && isNull( space ) && isNull( init ) )
                    {
                        comment.setKey( HEADER_KEY );

                        pxrProperties.setHeader( comment );

                        comment = new PxrComment();
                        comment.setKey( FOOTER_KEY );
                    }

                    comment.ingest( space,  init, text, eol );
                }
                else
                {
                    if ( nonNull( comment ))
                    {
                        comment.setKey( key );
                    }

                    PxrEntry pe = new PxrEntry();

                    pe.setComment( comment );
                    pe.setIndex( entryIndex++ );
                    pe.setKey( key );
                    pe.setSep( sep );
                    pe.setValue( text );
                    pe.setEol( eol );
                    pe.setContinuations( pecs );

                    pxrProperties.append( pe );

                    comment = null;
                }
                space = null;
                init = null;
                text = null;
                eol = false;
                key= null;
                pecs = null;
            }
        )+ | <EOF>
    )
    {
        pxrProperties.setFooter( comment );

        return pxrProperties;
    }
}

String initiator() :
{
    Token t = null;
}
{
    ( t = <BANG> | t = <HASH> )
    {
        return t.image;
    }
}


String text() :
{
    StringBuilder b = new StringBuilder( );
    Token t = null;
}
{
    (
        LOOKAHEAD(2)
        (
            t = <CHAR>
            | t = <SPACE>
            | t = <EQUALS>
            | t = <COLON>
            | t = <HASH>
            | t = <BANG>
            | t = "\\"
        )
        { b.append( t.image ); }
    )+
    {
        return b.toString();
    }
}

List<PxrEntryContinuation> continuations() :
{
    Token t = null;
    boolean eol = false;
    String space = null;
    String value = null;
    List<PxrEntryContinuation> pecs = new ArrayList<PxrEntryContinuation>();
    int index = 1;
}
{
    (
        (
            t = <CONT>
            (
                LOOKAHEAD(2)
                space = space()
                ( LOOKAHEAD(2) value = text() )?
            )?
            {
                PxrEntryContinuation pec = new PxrEntryContinuation( );

                pec.setIndex( index++ );
                pec.setCont( "\\" );
                if ( isNull( space ) && isNull( value ) )
                {
                    // e.g.: line continuation is followed by a comment on next line
                    pec.setEol( false );
                }
                else
                {
                    pec.setPrefix( space );
                    pec.setValue( value );
                    pec.setEol( true );
                }

                pecs.add( pec );

                space = null;
                value = null;
            }
        )
    )+
    {
        return pecs;
    }
}



String key() :
{
    Token t = null;
    StringBuilder b = new StringBuilder();
}
{
    (
        LOOKAHEAD(2)
        (
           t = <CHAR> | "\\" ( t = <SPACE> | t = <EQUALS> | t = <COLON> )
        )
        {
            b.append( t.image );
        }
    )+
    {
        return b.toString();
    }
}


String sep() :
{
    String spaceBefore = null;
    String spaceAfter = null;
    Token t = null;
}
{
    (
        LOOKAHEAD(2) ( spaceBefore = space() )? t = <EQUALS> ( LOOKAHEAD(2) spaceAfter = space() )?
        | LOOKAHEAD(2) ( spaceBefore = space() )? t = <COLON> ( LOOKAHEAD(2) spaceAfter = space() )?
        | ( spaceBefore = space() )
    )
    {
        return format(
                "%s%s%s",
                nonNull( spaceBefore ) ? spaceBefore : "",
                nonNull( t ) ? t.image : "",
                nonNull( spaceAfter ) ? spaceAfter : ""
        );
    }
}

String comment() :
{
    StringBuilder b = new StringBuilder( );
    Token t = null;
}
{
    (
        LOOKAHEAD(2)
        (
            t = <CHAR>
            | t = <SPACE>
            | t = <EQUALS>
            | t = <COLON>
            | t = <HASH>
            | t = <BANG>
            | t = "\\"
        )
        { b.append( t.image ); }
    )+
    {
        return b.toString();
    }
}



String space() :
{
    Token t = null;
}
{
    t = <SPACE>
    {
        return t.image;
    }
}

boolean eol() :
{
}
{
    ( <LF> | ( <CR> <LF> ) )
    {
        return true;
    }
}
